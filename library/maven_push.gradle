apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.malmstein'
ext.packaging = 'aar'
version = "0.0.1"

def isDevBuild
def isCiBuild
def isReleaseBuild

def sonatypeRepositoryUrl

if (hasProperty("release")) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else if (hasProperty("ci")) {
    isCiBuild = true
    version += "-SNAPSHOT"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
    isDevBuild = true
}

uploadArchives {
    configuration = configurations.archives
    repositories {
        if (isDevBuild) {
            mavenLocal()
        } else {
            mavenDeployer {

                if (isReleaseBuild) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }

                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: nexusUsername,
                            password: nexusPassword)
                }

                pom.project {
                    artifactId 'floatinghints'
                    name 'FloatingHints'
                    description 'Custom implementation of the Floating Label pattern for Android'
                    packaging 'aar'
                    url 'https://github.com/malmstein/FloatingHints'
                    inceptionYear '2014'

                    scm {
                        url 'https://github.com/malmstein/FloatingHints'
                        connection 'scm:https://github.com/malmstein/FloatingHints.git'
                        developerConnection 'scm:git@github.com:/malmstein/FloatingHints.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'malmstein'
                            name 'David Gonzalez'
                        }

                    }
                }
            }
        }
    }
}
